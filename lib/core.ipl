/*
 * Core IPL runtime.
 */

// The core runtime should not contain any definitions that require
// evaluation.

// -------------------------------------------------------------------
//  standard operators

syntax operator neg is right, unary, before appl;

syntax operator not is right, unary, prio appl;

syntax operator o is left, binary, after appl;
syntax operator comp is right, binary, after o;

syntax operator *   is left, binary, after comp;
syntax operator /   is left, binary, prio *;
syntax operator div is left, binary, prio *;
syntax operator mod is left, binary, prio *;

syntax operator + is left, binary, after *;
syntax operator - is left, binary, prio +;

syntax operator ^ is left, binary, after +;
syntax operator ## is left, binary, prio ^;

syntax operator :: is right, binary, after ##;

syntax operator <  is left, binary, after ::;
syntax operator <= is left, binary, prio <;
syntax operator >  is left, binary, prio <;
syntax operator >= is left, binary, prio <;

syntax operator =  is left, binary, after <;
syntax operator /= is left, binary, prio =;

syntax operator and is left, binary, after =;
syntax operator xor is left, binary, after and;
syntax operator or  is left, binary, after xor;

syntax operator , is right, binary, after or;

syntax operator >> is left, binary, after ,;
syntax operator << is left, binary, prio >>;

syntax block begin end;

syntax macrosep >>;
syntax macrosep <<;

let o = \f \g \x . f (g x);
let comp = \f \g \x . g (f x);
let not = \x . if x then false else true;
let xor = \x \y . (x or y) and not (x and y);
let neg = \x . 0 - x;
let /=  = \x \y . not (x = y);
let when = \&#x \&#y . if x then y else ();
let >> = \x \f . f x;
let << = \&#f . f;
let ## = join-tokens;

let matches = \x \y . xmatch x y \_ true \_ false;

macro unique-id = \args { #< #$0 ># };

let join-tokens-list = \toks {
   match toks with
   | %h :: %t ->
      join-tokens h (join-tokens-list t)
   | [] -> #< >#
};

let mk-tokens = \toks {
   join-tokens (join-tokens token-tokens-start (join-tokens-list toks)) token-tokens-end
};

let mk-list = \toks {
   let aux = \lst {
      match lst with
      | %h :: %t ->
         let r = aux t
         in
         #< , #h #r >#
      | [] -> #< ] >#
   };
   match toks with
   | %h :: %t ->
      let r = aux t
      in
      #< [ #h #r >#
   | [] -> #< [] >#
}

let mk-apply = \f \args {
   let z = join-tokens-list args
   in
   #< #f #z >#
};

macro(2) mk-call = \x \y {
   let z = mk-tokens [x]
   in
   #<mk-apply #z #y>#
};

macro mk-macro-call = \args { mk-call mk-call args }

let trace-with = \file \line \column \arg \&#block {
   let mkmsg =
      \_ . file ^ ":" ^ to_string line ^ ":" ^ to_string column ^ if arg = "" then "" else ": " ^ arg
   in
   try
      block
   with
   | %f when is-lambda f ->
      raise \_ { f (); print (mkmsg ()); }
   | %e ->
      raise \_ { print e; print ""; print "stacktrace:"; print ""; print (mkmsg ()); }
};

macro(1) trace = \block {
   #< trace-with #$file #$line #$column "" #block >#
};

macro try-apply = \args {
   if matches args (%_ :: %_) then {
      let toks = join-tokens-list args
      in
      #<
         begin
            try
               #toks
            with %_ -> ()
         end
      >#
   } else
      error "try-apply: wrong arguments"
};
