/* logic.ipl: Basic illative logic constructs.
 *
 * Copyright (C) 2013 by Lukasz Czajka
 */

import List;

syntax operator -> is right, binary, after appl;

syntax operator in is left, binary, after =;
syntax operator : is left, binary, after =;
syntax operator => is right, binary, after or;
syntax operator <=> is right, binary, after =>;

syntax block proof qed;

let error2 = \p \msg { error (msg ^ ": " ^ (to_string p)) }

symbol Type
symbol Prop

symbol ALL
symbol EX
symbol ASSM
symbol VAR

let in = \x \y . y x
let : = (in)
let -> = \t1 \t2 \f (ALL t1 \x . f x in t2)
let => = \x \y . if x then y else true
let <=> = \x \y . (x => y) and (y => x)
let proof-of = \x (`ipl-proof, x)
let goal = \p . match p with proof-of %x -> x | %_ -> error2 p "expected a proof"
let is-proof = \p . matches p (proof-of %_)

/*********************************************************************************/
/* helper functions & macros */

macro var-intro = \args {
   let v = unique-int-token ()
   in
   match args with
   | [%x, %b] ->
      #< ((\#x #b) '(VAR #v)) >#
   | [%x, #< in >#, %b] ->
      #< ((\#x #b) '(VAR #v)) >#
   | [%x] ->
      #< '(VAR #v) >> \#x .. >#
   | %_ -> error "var-intro: wrong arguments"
}

let mk-tactic-apply = \f \args { mk-apply f (#< assms ># :: #< facts ># :: args) }

macro(2) mk-tactic-call = \x \y {
   let z = mk-tokens [x]
   in
   #< mk-tactic-apply #z #y >#
}

macro(2) tactic = \id \eq-tok {
   if eq-tok /= #< = ># then
      error "tactic: expected '=' after tactic name"
   else {
      let tac-id = join-symbols id (join-symbols #< - ># #< tactic >#);
      #< macro #id = \args { mk-tactic-call #tac-id args }; let #tac-id = \assms \facts >#
   }
}

let eval-limit = 1000
let eq-limit = 100

let eq-aux = \f \x \y {
   let aux0 = \x \y \lim0 {
      let aux = \x \y \lim {
         if f x y then
            true
         else if lim = 0 then {
            let ex = eval-limited x eval-limit;
            let ey = eval-limited y eval-limit;
            if f ex ey then
               true
            else {
               match ex, ey with
               | '(%a1 %b1), '(%a2 %b2) ->
                  aux0 a1 a2 (lim0 - 1) and aux0 b1 b2 (lim0 - 1)
               | '(\x %_), '(\x %_) ->
                  var-intro x {
                     aux0 '(ex x) '(ey x) (lim0 - 1)
                  }
               | %_ ->
                  false
            }
         } else {
           let rx = reduce x
           let ry = reduce y
           let ex = reduce-eta x
           let ey = reduce-eta y
           in
           if f rx y or f x ry or f ex y or f ex ry or f x ey or f rx ey then
              true
           else
              aux rx ry (lim - 1)
         }
      };
      if lim0 = 0 then
         false
      else
         aux x y 2
   };
   aux0 x y eq-limit
}

let eq = eq-aux (=)

let qmatches = eq-aux matches

let is-in-list = \lst \x {
   List.exists (eq x) lst
}

let find-in-list = \lst \x {
   if is-in-list lst x then
      proof-of x
   else
      ()
}

let check-in-list = \lst \x \emsg {
   if is-in-list lst x then
      proof-of x
   else
      error2 x emsg
}

let match-in-list-acc = \lst \pat \acc {
   List.fold (\h . if qmatches h pat then (proof-of h) :: acc else acc) lst acc
}

let match-in-list = \lst \pat {
   match-in-list-acc lst pat []
}

let occurs-in-assms = \x \assms {
   let aux = \lst {
      match lst with
      | %h :: %t -> if occurs-check x h then true else aux t
      | [] -> false
   };
   aux assms
}

let mk-raw-apply = \f \args { mk-apply f (#< assms ># :: args) }

macro(2) mk-raw-call = \x \y {
   let z = mk-tokens [x]
   in
   #< mk-raw-apply #z #y >#
}


/*********************************************************************************/
/* basic proof methods */

let pexpand = \a \p {
   let phi = goal p
   in
   if eq phi (reduce a) or eq phi (reduce-eta a) then
      proof-of a
   else
      error2 (phi, a) "cannot expand"
}

let preduce = \p {
   proof-of (reduce (goal p))
}

let preduce-eta = \p {
   proof-of (reduce-eta (goal p))
}

let peval = \p {
   proof-of (eval (goal p))
}

let peval-limited = \p \lim {
   proof-of (eval-limited (goal p) lim)
}

let make-assumption-raw = \assms \phi \f {
   let p = f (phi :: assms)
   in
   match p with
   | proof-of %x -> proof-of '(ASSM phi x)
   | %_ -> error2 p "expected a proof"
}

macro make-assumption = \args {
   match args with
   | [%a] ->
      #< make-assumption-raw assms #a \assms .. >#
   | [%a, %b] ->
      #< make-assumption-raw assms #a \assms { #b } >#
   | [%a, #< in >#, %b] ->
      #< make-assumption-raw assms #a \assms { #b } >#
   | [%name, #< : >#, %a] ->
      #< let #$0 = #a; let #name = proof-of #$0; make-assumption-raw assms #$0 \assms .. >#
   | [%name, #< : >#, %a, %b] ->
      #< let #$0 = #a; let #name = proof-of #$0; make-assumption-raw assms #$0 \assms { #b } >#
   | [%name, #< : >#, %a, #< in >#, %b] ->
      #< let #$0 = #a; let #name = proof-of #$0; make-assumption-raw assms #$0 \assms { #b } >#
   | %_ -> error "make-assumption: wrong arguments"
}

macro suppose = \args { mk-macro-call make-assumption args }

let impl-intro = \p1 \p2 {
   match goal p1, goal p2 with
   | ('(%b in Prop), '(ASSM %a %x)) ->
      if eq a b then
         proof-of '(a => x)
      else
         error2 p1 "impl-intro: bad typing derivation"
   | %_ -> error2 (p1, p2) "impl-intro: expected a typing derivation and a supposition"
}

let impl-elim = \p1 \p2 {
   let form = goal p2
   in
   match goal p1 with
   | '(form => %p) -> proof-of p
   | %_ -> error2 p1 "impl-elim: expected implication"
}

let impl-type-intro = \p1 \p2 {
   match goal p1, goal p2 with
   | '(%phi1 in Prop), '(ASSM %phi2 (%psi in Prop)) ->
      if eq phi1 phi2 then
         proof-of '((phi1 => psi) in Prop)
      else
         error2 phi2 "impl-type-intro: wrong argument"
   | '(%phi in Prop), '(%psi in Prop) -> proof-of '((phi => psi) in Prop)
   | %_ -> error2 (p1, p2) "impl-type-intro: wrong arguments"
}

let impl-type-elim = \p {
   match goal p with
   | '((%a => %_) in Prop) -> proof-of '(a in Prop)
   | %_ -> error2 p "impl-type-elim: wrong argument"
}

macro assume-with = \args {
   match args with
   | [%a, %t] ->
      #< impl-intro #t << make-assumption-raw assms #a \assms .. >#
   | [%name, #< : >#, %a, %t] ->
      #< let #$0 = #a; let #name = proof-of #$0; impl-intro #t << make-assumption-raw assms #$0 \assms .. >#
   | [%a, %t, %b] ->
      #< impl-intro #t (suppose #a #b) >#
   | [%a, %t, #< in >#, %b] ->
      #< impl-intro #t (suppose #a #b) >#
   | [%name, #< : >#, %a, %t, %b] ->
      #< let #$0 = #a in impl-intro #t (suppose #$0 { let #name = proof-of #$0 in #b }) >#
   | [%name, #< : >#, %a, %t, #< in >#, %b] ->
      #< let #$0 = #a in impl-intro #t (suppose #$0 { let #name = proof-of #$0 in #b }) >#
   | %_ ->
      error "assume-with: wrong arguments"
}

macro assume = \args {
   match args with
   | [%a] ->
      #< let #$0 = #a in assume-with #$0 (auto-type '(#$0 in Prop)) >#
   | [%name, #< : >#, %a] ->
      #< let #$0 = #a in assume-with #name : #$0 (auto-type '(#$0 in Prop)) >#
   | [%a, %b] ->
      #< let #$0 = #a in assume-with #$0 (auto-type '(#$0 in Prop)) #b >#
   | [%a, #< in >#, %b] ->
      #< let #$0 = #a in assume-with #$0 (auto-type '(#$0 in Prop)) #b >#
   | [%name, #< : >#, %a, %b] ->
      #< let #$0 = #a in assume-with #name : #$0 (auto-type '(#$0 in Prop)) #b >#
   | [%name, #< : >#, %a, #< in >#, %b] ->
      #< let #$0 = #a in assume-with #name : #$0 (auto-type '(#$0 in Prop)) #b >#
   | [%a, #< with >#, %t, %b] ->
      #< assume-with #a #t #b >#
   | [%a, #< with >#, %t, #< in >#, %b] ->
      #< assume-with #a #t #b >#
   | [%a, #< with >#, %t] ->
      #< assume-with #a #t >#
   | [%name, #< : >#, %a, #< with >#, %t, %b] ->
      #< assume-with #name : #a #t #b >#
   | [%name, #< : >#, %a, #< with >#, %t, #< in >#, %b] ->
      #< assume-with #name : #a #t #b >#
   | [%name, #< : >#, %a, #< with >#, %t] ->
      #< assume-with #name : #a #t >#
   | %_ ->
      error "assume: wrong arguments"
}

let forall-intro-raw = \assms \p1 \p2 {
   match goal p1, goal p2 with
   | ('(%a1 in Type), '(ASSM ((VAR %n) in %a2) %app)) when eq a1 a2 ->
      if occurs-in-assms '(VAR n) assms or occurs-check '(VAR n) a1 then
         error2 (p1, p2) "forall-intro: the variable quantified over cannot occur in the assumptions"
      else {
         match lift app '(VAR n) with
         | '(%phi (VAR n)) -> proof-of '(ALL a1 phi)
         | %_ -> error2 p2 "forall-intro: bad proof"
      }
   | %_ -> error2 (p1, p2) "forall-intro: wrong arguments"
}

macro forall-intro = \args { mk-raw-call forall-intro-raw args }

let forall-elim = \p1 \p2 {
   match goal p1, goal p2 with
   | ('(ALL %a %phi), '(%t in %a1)) when eq a a1 ->
      proof-of '(phi t)
   | %_ -> error2 (p1, p2) "forall-elim: wrong arguments"
}

let forall-type-intro-raw = \assms \p1 \p2 {
   match goal p1, goal p2 with
   | ('(%a1 in Type), '(ASSM ((VAR %n) in %a2) (%app in Prop))) when eq a1 a2 ->
      if occurs-in-assms '(VAR n) assms or occurs-check '(VAR n) a1 then
         error2 (p1, p2) "forall-type-intro: the variable quantified over cannot occur in the assumptions"
      else {
         match lift app '(VAR n) with
         | '(%phi (VAR n)) -> proof-of '(ALL a1 phi in Prop)
         | %_ -> error2 p2 "forall-type-intro: bad proof"
      }
   | %_ -> error2 (p1, p2) "forall-type-intro: wrong arguments"
}

macro forall-type-intro = \args { mk-raw-call forall-type-intro-raw args }

/* assumption: '(VAR varid) does not occur in assms */
let forall-fix-raw = \assms \varid \p \f {
   match goal p with
   | '(%a in Type) ->
      begin
         let p1 = f '(VAR varid) ('((VAR varid) in a) :: assms)
         in
         match lift (goal p1) '(VAR varid) with
         | '(%phi (VAR varid)) -> proof-of '(ALL a phi)
         | %_ -> error2 p1 "forall-fix: bad proof"
      end
   | %_ -> error2 p "forall-fix: bad typing"
}

macro fix-with = \args {
   match args with
   | [%x, %p] ->
      let varid = unique-int-token ()
      in
      #< forall-fix-raw assms #varid #p \#x \assms .. >#
   | [%x, %p1, %p2] ->
      let varid = unique-int-token ()
      in
      #< forall-fix-raw assms #varid #p1 (\#x \assms { #p2 }) >#
   | [%x, %p1, #< in >#, %p2] ->
      let varid = unique-int-token ()
      in
      #< forall-fix-raw assms #varid #p1 (\#x \assms { #p2 }) >#
   | %_ -> error "fix: bad arguments"
}

macro fix = \args {
   match args with
   | [%x, #< : >#, %a] ->
      #< fix-with #x (auto-type '(#a in Type)) >#
   | [%x, #< : >#, %a, %p] ->
      #< fix-with #x (auto-type '(#a in Type)) #p >#
   | [%x, #< : >#, %a, #< in >#, %p] ->
      #< fix-with #x (auto-type '(#a in Type)) #p >#
   | [%x, #< : >#, %a, #< with >#, %t] ->
      #< fix-with #x #t >#
   | [%x, #< : >#, %a, #< with >#, %t, %p] ->
      #< fix-with #x #t #p >#
   | [%x, #< : >#, %a, #< with >#, %t, #< in >#, %p] ->
      #< fix-with #x #t #p >#
   | %_ ->
      error "fix: bad arguments (maybe you forgot parentheses around the type)"
}

let exists-intro = \p1 \p2 \p3 {
   match (goal p1, goal p2, goal p3) with
   | ('(%a in Type), '(%t in %a1), '(%phi %t1)) when eq a a1 and eq t t1 ->
      '(EX a phi)
   | %_ -> error2 (p1, p2, p3) "exists-intro: wrong arguments"
}

let exists-elim-raw = \assms \p1 \p2 {
   match (goal p1, goal p2) with
   | ('(EX %a %phi), '(ASSM ((VAR %n) in %a1) (ASSM %app %psi))) when eq a a1 ->
      if occurs-in-assms '(VAR n) assms or occurs-check '(VAR n) a or occurs-check '(VAR n) psi then
         error2 (p1, p2) "exists-elim: the variable quantified over cannot occur in the assumptions or in the conclusion"
      else {
         match lift app '(VAR n) with
         | '(%phi1 (VAR n)) when eq phi phi1 -> proof-of psi
         | %_ -> error2 p2 "exists-elim: bad proof"
      }
   | %_ -> error2 (p1, p2) "exists-elim: wrong arguments"
}

macro exists-elim = \args { mk-raw-call exists-elim-raw args }

/* assumption: '(VAR v) does not occur in assms */
let exists-obtain = \assms \v \p \f {
   match goal p with
   | '(EX %alpha %phi) ->
      begin
         let p1 = f '(VAR v) ('(phi (VAR v)) :: '((VAR v) in alpha) :: assms)
         in
         let psi = goal p1
         in
         if occurs-check '(VAR v) psi then
            error2 psi "exists-obtain: the variable quantified over cannot occur in the conclusion"
         else
            proof-of psi
      end
   | %_ -> error2 p "exists-obtain: wrong argument"
}

macro obtain = \args {
   let v = unique-int-token ()
   in
   match args with
   | [%name, #< from >#, %p, #< in >#, %b] ->
      #< exists-obtain assms #v #p (\#name \assms #b) >#
   | [%name, #< from >#, %p, %b] ->
      #< exists-obtain assms #v #p (\#name \assms #b) >#
   | [%name, #< from >#, %p] ->
      #< exists-obtain assms #v #p << \#name \assms .. >#
   | [%name, %p, %b] ->
      #< exists-obtain assms #v #p (\#name \assms #b) >#
   | [%name, %p] ->
      #< exists-obtain assms #v #p << \#name \assms .. >#
   | %_ -> error "obtain: wrong arguments"
}

let exists-type-intro-raw = \assms \p1 \p2 {
   match goal p1, goal p2 with
   | ('(%a1 in Type), '(ASSM ((VAR %n) in %a2) (%app in Prop))) when eq a1 a2 ->
      if occurs-in-assms '(VAR n) assms or occurs-check '(VAR n) a1 then
         error2 (p1, p2) "exists-type-intro: the variable quantified over cannot occur in the assumptions"
      else {
         match lift app '(VAR n) with
         | '(%phi (VAR n)) -> proof-of '(EX a1 phi in Prop)
         | %_ -> error2 p2 "exists-type-intro: bad proof"
      }
   | %_ -> error2 (p1, p2) "exists-type-intro: wrong arguments"
}

macro exists-type-intro = \args { mk-raw-call exists-type-intro-raw args }

let or-intro-left = \p \right {
   let left = goal p
   in
   proof-of '(left or right)
}

let or-intro-right = \left \p {
   let right = goal p
   in
   proof-of '(left or right)
}

let or-intro = \x \y {
   match x with
   | proof-of %phi -> or-intro-left x y
   | %_ -> or-intro-right x y
}

let or-elim = \p \p1 \p2 {
   match (goal p, goal p1, goal p2) with
   | ('(%a or %b), '(ASSM %a1 %c), '(ASSM %b1 %c1)) when eq a a1 and eq b b1 and eq c c1 ->
      proof-of c
   | %_ -> error2 (p, p1, p2) "or-elim: wrong arguments"
}

let or-type-intro = \p1 \p2 {
   match goal p1, goal p2 with
   | '(%phi in Prop), '(%psi in Prop) -> proof-of '((phi or psi) in Prop)
   | %_ -> error2 (p1, p2) "or-type-intro: wrong arguments"
}

let and-intro = \p1 \p2 {
   let a = goal p1
   let b = goal p2
   in
   proof-of '(a and b)
}

let and-elim-left = \p {
   match goal p with
   | '(%a and %_) -> proof-of a
   | %_ -> error2 p "and-elim-left: wrong argument"
}

let and-elim-right = \p {
   match goal p with
   | '(%_ and %b) -> proof-of b
   | %_ -> error2 p "and-elim-right: wrong argument"
}

let and-type-intro = \p1 \p2 {
   match goal p1, goal p2 with
   | '(%a in Prop), '(%b in Prop) -> proof-of '((a and b) in Prop)
   | %_ -> error2 (p1, p2) "and-type-intro: wrong arguments"
}

let equiv-intro = \p1 \p2 {
   match goal p1, goal p2 with
   | '(%phi => %psi), '(%psi1 => %phi1) when eq phi phi1 and eq psi psi1 -> proof-of '(phi <=> psi)
   | %_ -> error2 (p1, p2) "equiv-intro: wrong arguments"
}

let equiv-type-intro = \p1 \p2 {
   match goal p1, goal p2 with
   | '(%phi in Prop), '(%psi in Prop) ->
      proof-of '((phi <=> psi) in Prop)
   | '(ASSM %psi1 (%phi in Prop)), '(ASSM %phi1 (%psi in Prop)) when eq psi psi1 and eq phi phi1 ->
      proof-of '((phi <=> psi) in Prop)
   | '(ASSM %psi1 (%phi in Prop)), '(%psi in Prop) when eq psi psi1 ->
      proof-of '((phi <=> psi) in Prop)
   | '(%phi in Prop), '(ASSM %phi1 (%psi in Prop)) when eq phi phi1 ->
      proof-of '((phi <=> psi) in Prop)
   | %_ ->
      error2 (p1, p2) "equiv-type-intro: wrong arguments"
}

let equiv-elim = \p1 \p2 {
   match goal p1 with
   | '(%phi <=> %psi) when eq phi (goal p2) -> proof-of psi
   | '(%phi <=> %psi) when eq psi (goal p2) -> proof-of phi
}

let false-elim = \phi \p {
   match p with
   | proof-of false -> proof-of phi
   | %_ -> error2 p "false-elim: wrong argument"
}

let not-intro-raw = \p1 \p2 {
   match (goal p1, goal p2) with
   | ('(%a in Prop), '(ASSM %a1 false)) when eq a a1 ->
      proof-of '(not a)
   | %_ -> error2 (p1, p2) "not-intro: wrong arguments"
}

macro(1) not-intro = \p #<{
   let #$0 = #p
   in
   match goal #$0 with
   | '(ASSM %a false) ->
      not-intro-raw (auto-type '(a in Prop)) #$0
   | '(%a => false) ->
      proof-of '(not a)
   | %_ -> error2 #$0 "not-intro: wrong argument"
}>#

let not-elim = \p1 \p2 {
   match (goal p1, goal p2) with
   | ('%a, '(not %a1)) when eq a a1 ->
      proof-of false
   | ('(not %a), '%a1) when eq a a1 ->
      proof-of false
   | %_ -> error2 (p1, p2) "not-elim: wrong arguments"
}

let not-type-intro = \p {
   match goal p with
   | '(%g in Prop) -> proof-of '((not g) in Prop)
   | %_ -> error2 p "not-type-intro: wrong argument"
}

let not-type-elim = \p {
   match goal p with
   | '((not %a) in Prop) -> proof-of '(a in Prop)
   | %_ -> error2 p "not-type-elim: wrong argument"
}

let contradiction = \p {
   match goal p with
   | '((not %x) => false) -> proof-of x
   | '(not (not %x)) -> proof-of x
   | '((%x => false) => false) -> proof-of x
   | %_ -> error2 p "contradiction: wrong argument"
}

let prop-intro = \p {
   let phi = goal p
   in
   proof-of '(phi in Prop)
}

let prop-type-intro = proof-of '(Prop in Type)

let fun-type-intro = \p1 \p2 {
   match goal p1, goal p2 with
   | '(%a in Type), '(%b in Type) -> proof-of '((a -> b) in Type)
   | %_ -> error2 (p1, p2) "fun-type-intro: wrong arguments"
}

let equal-intro = \a \b {
   if eq a b then
      proof-of '(a = b)
   else
      error2 (a, b) "equal-intro: not equal"
}

let equal-elim = \p1 \p2 {
   match goal p1, goal p2 with
   | '(%a = %b), '(%phi %b1) when eq b b1 -> proof-of '(phi a)
   | %_ -> error2 (p1, p2) "equal-elim: wrong arguments"
}

let equal-sym = \p {
   match goal p with
   | '(%a = %b) -> proof-of '(b = a)
   | %_ -> error2 p "equal-sym: wrong argument"
}

let equal-trans = \p1 \p2 {
   match goal p1, goal p2 with
   | '(%a = %b), '(%b1 = %c) when eq b b1 -> proof-of '(a = c)
   | %_ -> error2 (p1, p2) "equal-trans: wrong arguments"
}

let equal-cong = \p1 \p2 {
   match goal p1, goal p2 with
   | '(%a = %b), '(%c = %d) -> proof-of '(a c = b d)
   | %_ -> error2 (p1, p2) "equal-cong: wrong arguments"
}

let bool-ext-true = \p {
   let g = goal p
   in
   proof-of '(g = true)
}

let bool-ext-false = \p {
   match goal p with
   | '(not %a) -> '(a = false)
   | %_ -> error2 p "bool-ext-false: wrong argument"
}

macro(1) assumption = \a #< check-in-list assms #a "not an assumption" >#
macro assm = \args { mk-macro-call assumption args }
macro(1) fact = \phi #< check-in-list facts #phi "not a proven fact" >#

macro(1) known = \phi #< {
   let #$0 = #phi;
   if is-in-list assms #$0 or is-in-list facts #$0 then
      proof-of #$0
   else
      error2 #$0 "not a known fact"
} >#

macro(1) is-assm = \x #< is-in-list assms #x >#
macro(1) is-assumption = \x #< is-in-list assms #x >#
macro(1) is-fact = \x #< is-in-list facts #x >#
macro(1) is-known = \x #< { let #$0 = #x in is-in-list assms #$0 or is-in-list facts #$0 } >#

macro(1) find-assm = \x #< find-in-list assms #x >#
macro(1) find-assumption = \x #< find-in-list assms #x >#
macro(1) find-fact = \x #< find-in-list facts #x >#

macro(1) find-known = \x #<{
   let #$0 = #x;
   let #$1 = find-in-list assms #$0;
   if #$1 = () then
      find-in-list facts #$0
   else
      #$1
}>#

macro(1) match-assms = \pat #< match-in-list assms #pat >#
macro match-assumptions = \args { mk-macro-call match-assms args }
macro(1) match-facts = \pat #< match-in-list facts #pat >#
macro(1) match-known = \pat #< { let #$0 = #pat in match-in-list-acc facts #$0 (match-in-list assms #$0) } >#

let false-type-intro = proof-of '(false in Prop)
let true-type-intro = proof-of '(true in Prop)

let true-intro = proof-of true

let faith = \phi { proof-of phi }

/*********************************************************************************/

let assms = []
let facts = []

/*********************************************************************************/

let verify = \phi \p {
   if eq (goal p) phi then
      p
   else
      error2 p ("not a proof of " ^ to_string phi)
}

/*********************************************************************************/
/* convenience macros */

macro lemma = \args {
   match args with
   | [%name, %formula, %xproof] ->
      #<
         let #$0 = #formula;
         let #name = { let thesis = #$0; #xproof };
         verify #$0 #name;
         (#$0 :: facts) >> \facts ..
      >#
   | [%formula, %xproof] ->
      #<
         let #$0 = #formula;
         let #$1 = { let thesis = #$0; #xproof };
         verify #$0 #$1;
         (#$0 :: facts) >> \facts ..
      >#
   | %_ -> error "lemma: wrong arguments"
}

macro theorem = \args { mk-macro-call lemma args }

macro axiom = \args {
   match args with
   | [%phi] -> #< (#phi :: facts) >> \facts .. >#
   | %_ -> error "axiom: wrong arguments"
}

let ex-all-aux = \args \quant \msg {
   match List.split2 (=) args [#< : >#, #< . >#] with
   | [%lst1, %lst2, %lst3] ->
      begin
         let tp =
            if lst3 = [] then
               join-tokens-list (List.head lst2 (List.length lst2 - 1))
            else
               join-tokens-list lst2
         in
         let rest =
            if lst3 = [] then
               List.tail lst2 1
            else
               join-tokens-list lst3
         in
         let aux = \lst {
            match lst with
            | [%x] :: %t ->
               join-tokens #< #quant (#tp) \#x . ># (aux t)
            | [] -> #< >#
            | %_ -> error msg
         };
         let h = aux (List.split (\x . x = #< , >#) lst1)
         in
         #< #h #rest >#
      end
   | %_ -> error msg
}

macro ex = \args { ex-all-aux args #< EX ># "exists: wrong arguments" }
macro all = \args { ex-all-aux args #< ALL ># "forall: wrong arguments" }

macro exists = \args { mk-macro-call ex args }
macro forall = \args { mk-macro-call all args }

/*********************************************************************************/
/* core proof tactics */

include "tactics/auto-type.ipl"
